apply plugin: "maven"
apply from: "$project.rootDir/config.gradle"

// 打包源码
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
    //archives javadocJar
}

Properties properties = new Properties()
File localPropertiesFile = rootProject.file("local.properties")
if (localPropertiesFile.exists()) {
    properties.load(localPropertiesFile.newDataInputStream())
}
// 用户名
def mavenUser = properties.getProperty("maven.user")
def mavenPass = properties.getProperty("maven.pass")
def uploadUrl = properties.getProperty("maven.url")
uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: uri(uploadUrl)){
                //配置账号密码
                authentication(userName: mavenUser, password: mavenPass)
            }
            android.libraryVariants.all { variant ->
                def isRelease = variant.buildType.name == "release"

                //只上传release的，如果没有多个flavor的去掉isFlavor的判断
                if(isRelease){
                    pom.version = android.defaultConfig.versionName
                    pom.groupId = repos.groupId
                    pom.artifactId = POM_ARTIFACT_ID
                }
            }
        }
    }
}